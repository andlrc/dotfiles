" vim: fdm=marker
" Author Andreas Louv <andreas@louv.dk>

" Options {{{1

filetype plugin indent on
syntax enable
set synmaxcol=200

set history=1000
set hidden
set visualbell belloff=wildmode
set backspace=indent,eol,start
set nrformats-=octal

set undofile undodir=~/.vim/tmp/undo//
set dir=~/.vim/tmp/swap//

set listchars+=tab:\|\ 

" Leader {{{1

let mapleader = "\<Space>"
let maplocalleader = "\\"

" Searching and Navigating Files {{{1

setglobal ignorecase smartcase hlsearch incsearch
setglobal path^=**
setglobal grepprg=grep\ -nH\ --exclude='*.o'\ --exclude='*.tags'
                     \\ --exclude='tags'\ --exclude-dir=node_modules
                     \\ --exclude-dir=.git\ $*

nnoremap <leader>g :grep<space>
nnoremap <leader>f :g//#<left><left>
nnoremap gb :ls<cr>:b<space>

" Use "g*" to make a more perminent highlight of a word {{{2

nnoremap <silent> g* :call <SID>MatchAdd(expand("<cword>"))<cr>
let s:MatchAdd_words = {}
function! s:MatchAdd(word)
  if has_key(s:MatchAdd_words, a:word)
    try
      call matchdelete(s:MatchAdd_words[a:word])
      unlet s:MatchAdd_words[a:word]
      return
    catch /E803/
    endtry
  endif
  let regex = '\<' . a:word . '\>'
  let s:MatchAdd_words[a:word] = matchadd('SpellCap', regex)
  let @/ = regex
  norm! n
endfunction

" Power <CR> {{{2

cnoremap <expr> <CR> getcmdline() =~# '[vg]/.*/#$'
                   \ ? '<CR>:call <SID>PromptAndExec(":%d\r")<CR>'
                   \ : '<CR>'

" Prompt And Exec Function {{{2

function! s:PromptAndExec(cmd)
  let ans = input('Type number and <Enter> (empty cancels): ')

  if ans !~ '^\s*$'
    try
      silent execute 'normal! ' . printf(a:cmd, ans)
    catch /E387/ "E387: Match is on current line
    endtry
  endif
endfunction

" Faster define and include {{{2

nnoremap [I [I:call <SID>PromptAndExec(":ijump! %d \022\027\015")<CR>
nnoremap ]I ]I:call <SID>PromptAndExec(":+1,$ijump! %d \022\027\015")<CR>

nnoremap [D [D:call <SID>PromptAndExec(":djump %d \022\027\015")<CR>
nnoremap ]D ]D:call <SID>PromptAndExec(":+1,$djump %d \022\027\015")<CR>

" Enhanced % {{{2

packadd! matchit

" Spelling {{{1

set spellfile=~/.vim/spell.utf-8.add

" zg will add <cword> to the custom spell file, sort that file after adding
nnoremap zg zg:silent ! sort ~/.vim/spell.utf-8.add -o
                           \ ~/.vim/spell.utf-8.add<cr>:redraw!<cr>

" Completion {{{1

set wildmenu
set wildmode=longest,full
set wildignorecase
set suffixes^=,			" Files without extension
set suffixes-=.h
set completeopt-=preview

" Wrapping {{{1

" Why can't I set "nowrap" nor "textwidth" with "setglobal",
" "/usr/share/vim/vimfiles/archlinux.vim" doesn't set them.
set nowrap
set textwidth=80
set formatoptions+=jo
set formatoptions-=t

" Abbreviations {{{1

inorea <al@ <andreas@louv.dk
inorea al@ andreas@louv.dk

" Convenience mappings {{{1

nnoremap <silent> <C-l> :noh<cr>:call clearmatches()<cr><C-l>

nnoremap <silent> <leader>cc
      \ :let &l:colorcolumn = &l:colorcolumn == '' ? '+0' : ''<CR>

nnoremap <silent> <leader>ss
      \ :echo synIDattr(synID(line('.'), col('.'), 0), 'name')<CR>

nnoremap <silent> <leader>ww
      \ mz:keepp %s/\\\@<!\s\+$//e<cr>`z

" Folding {{{1

" Automatic set foldmethod to manual when entering insert mode, this is done
" to prevent vim from unfolding automatic folds, when the code changes.
augroup LazyFolds
  autocmd!
  autocmd InsertEnter,WinLeave * :call <SID>LazyFolds(0)
  autocmd InsertLeave,WinEnter * :call <SID>LazyFolds(1)
augroup END

function! s:LazyFolds(type)
  if a:type == 0
    let b:lazy_foldmethod=&foldmethod
    setlocal foldmethod=manual
  elseif exists('b:lazy_foldmethod')
    let &l:foldmethod = b:lazy_foldmethod
  endif
endfunction

" Color Fixes {{{1

highlight Folded term=standout ctermfg=7 ctermbg=0
highlight! link Conceal PreProc

" FileType Detect {{{1

augroup FT_Detect
  autocmd!
  autocmd BufNewFile,BufRead *.widget setlocal filetype=javascript.widget
  autocmd BufNewFile,BufRead *.{xjson,xpd,ns,dmd,lib} setlocal filetype=json
  autocmd BufNewFile,BufRead *.tpl setlocal filetype=html
  autocmd BufNewFile,BufRead *.as[pm]x setlocal filetype=rpgle
  autocmd BufNewFile,BufRead *.qdmd setlocal filetype=qdmd
augroup END

" UndotreeToggle {{{1

let g:undotree_SetFocusWhenToggle = 1
